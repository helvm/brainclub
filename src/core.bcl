\ Core BrainClub definitions
\ This file is public domain

: )_DOTSTRING ( 1+ )$1 OUTPUT ( 1- )$1 ;
: )#NUM# 0 ( 1+ )$1 ;
: )#STR#. 0 ( $1 )#STRING#_DOTSTRING DROP ;
: 0 {>[-]} ;
: ! {[-]~*[-~*+~*]<~*} ;
: @ {*~>[-]*[-]~[-~+*+*~]~[-~+~]~} ;
: @Z {*~>[-]~*[-~*+*~]} ;
: - {[-<->]<} ;
: + {[-<+>]<} ;
: * {>[-]>[-]<<<[->>>+<<<]>>>[<<[-<+>>+<]>[-<+>]>-]<<<} ;
: = - 0= ;
: )= ( {-} )$1 0= ;
: < SWAP > ;
: <= > 0= ;
: > {+<+>} 1 {[<-<->} OVER 0= OVER 0= NOR {]<<} ;
: >= < 0= ;
: )#STR#? ( $1 )#STRING# ;
: ?DUP DUP IF DUP THEN ;
: 0= {>[-]+<[>[-]<[-]]>[-<+>]<} ;
: 0EMPH 15 EMIT ;
: 0FLAG ( 0 )FLAG ;
: 1- {-} ;
: 1+ {+} ;
: 1EMPH 14 EMIT ;
: 1FLAG ( 1 )FLAG ;
: 2* {>[-]<[->++<]>[-<+>]<} ;
: 2DUP OVER OVER ;
: AGAIN {>[-]+]} ;
: AND NAND 0= ;
: BACKSPACE 8 EMIT ;
: BEGIN {>[-]+[<} ;
: BELL 7 EMIT ;
: CLS 12 EMIT ;
: CR 10 EMIT ;
: DROP {<} ;
: DUP {>[-]>[-]<<[->>+<<]>>[-<+<+>>]<} ;
: ELSE 0FLAG THEN FLAG@ IF ;
: EMIT {.<} ;
: )FLAG {~[-]} ( {+} )$1 {~} ;
: FLAG! {~[-]~[-~+~]<} ;
: FLAG@ {>[-]~[-~+~]~} ;
: IF 1FLAG {[<} ;
: INDEX 0FLAG {[-~<[-]+~]<} ;
: INPUT {>,} ;
: ITERATE {>[} ;
: LSB {>[-]<[->} ( 1+ )128 {<]>[<+>[-]]<} ;
: NAND {>[-]--<[[-]>+<]<[[-]>>+<<]>>[-<<+>>]<<} ;
: NEG {>[-]<[->-<]>[-<+>]<} ;
: NEXT {>]} ;
: NOR {>[-]+<[>[-]<[-]]<[>>[-]<<[-]]>>[-<<+>>]<<} ;
: OR NOR 1- ;
: OUTDEX {~[>]~} ;
: OUTPUT {.} ;
: OVER {>[-]>[-]<<<[->>>+<<<]>>>[-<+<<+>>>]<} ;
: )PICK 0 0 {<<} ( {<} )$1 {[->>} ( {>} )$1 {+<<} ( {<} )$1 {]>>} ( {>} )$1 {[-<+<} ( {<} )$1 {+>>} ( {>} )$1 {]<} ;
: REPEAT 1FLAG THEN FLAG@ {]<} ;
: REWIND {[<]} ;
: SWAP {<[->>+<<]>[-<+>]>[-<+>]<} ;
: THEN {>[-]]<} ;
: UNTIL 0= {]<} ;
: )VAR $: )$1 {~} ( {<} )$=HERE {*} $$* {*} ( {>} )$=HERE {~} $; ( HERE $2 )#ADD# ;
: WHILE 0FLAG {[<} ;
: ZERO {[-]} ;

( HERE 1 )#SET#
{~<~}
